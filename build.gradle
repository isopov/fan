buildscript {
   repositories {
      mavenCentral()
   }
   dependencies {
      classpath 'com.github.townsfolk:gradle-release:1.2'
   }
}

apply plugin: 'java'
apply plugin: 'release'

sourceCompatibility = 1.7

ext {
    slf4jVersion = "1.7.5"
    springVersion = "3.2.0.RELEASE"
    logbackVersion = "1.0.11"
    querydslVersion = "3.1.1"
    springSecurityVersion = "3.1.4.RELEASE"
    springSocialVersion = "1.1.0.M2"
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse-wtp'

    repositories {
        mavenCentral()
        maven {
            url "http://repo.springsource.org/milestone"
        }
    }

    configurations.all {
        it.exclude group: 'log4j'
        it.exclude group: 'commons-logging'
        
        resolutionStrategy {
            failOnVersionConflict()

            force "org.slf4j:slf4j-api:$slf4jVersion"
            force "org.springframework:spring-core:$springVersion"
            force "org.springframework:spring-context:$springVersion"
            force "org.springframework:spring-beans:$springVersion"
            force "org.springframework:spring-jdbc:$springVersion"
            force "org.springframework:spring-expression:$springVersion"
            force "org.springframework:spring-aop:$springVersion"
            force "org.springframework:spring-tx:$springVersion"
            force "org.springframework:spring-orm:$springVersion"
            force "org.springframework:spring-web:$springVersion"
            force "org.springframework:spring-webmvc:$springVersion"
            force "org.springframework.security:spring-security-web:$springSecurityVersion"
            //For hibernate internal dependency convergence
            force 'org.jboss.logging:jboss-logging:3.1.3.GA'
            force "org.slf4j:slf4j-api:$slf4jVersion"
            force "org.slf4j:jcl-over-slf4j:$slf4jVersion"
            force 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
            force 'com.google.guava:guava:14.0.1'
        }
    }

    dependencies {

        compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        //compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
        compile "org.slf4j:log4j-over-slf4j:$slf4jVersion"

        compile("ch.qos.logback:logback-classic:$logbackVersion")

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'

        compile(group: 'org.springframework', name: 'spring-test', version:"$springVersion") {
            exclude(module: 'spring-webmvc')
        }
    }

    group = 'com.sopovs.moradanen'
}
